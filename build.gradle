subprojects {
    buildscript {
        repositories {
            mavenLocal()
            jcenter()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
            mavenCentral()
        }
        dependencies {
            classpath "gradle.plugin.org.frege-lang:frege-gradle-plugin:0.7"
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
            classpath 'commons-io:commons-io:2.4'
        }
    }

    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/groups/public" }
    }

    pluginManager.withPlugin('java') {
        targetCompatibility = libraries.jdk
        sourceCompatibility = libraries.jdk
    }

    pluginManager.withPlugin('maven') {
        group = metadata.group
        version = metadata.version
    }

    pluginManager.withPlugin('org.frege-lang') {
        compileFrege {
            target = libraries.jdk
        }

        compileTestFrege {
            target = libraries.jdk
        }
    }
}

ext.metadata = [
    group   : "com.github.fregelab",
    version : "0.1.2",
    desc: 'Diablo is a Frege abstraction to support different JVM template engines'
]

ext.libraries = [
    jdk: '1.7',
    frege_core: 'org.frege-lang:frege:3.24-7.100',
    frege_repl: "org.frege-lang:frege-repl-core:1.3"
]
